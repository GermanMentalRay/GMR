//
// creator: sciloop
//
// Show in the channel box all relevant transform attributes.
// Select the transform nodes.
//
// last changed: 2014_09_04
//
// sciDisplayCameraAttributesInChannelBox( {} );
//
//_______________________________________________________________________________
//
//
global proc sciDisplayCameraAttributesInChannelBox( string $inputArray[] )
{

	string $sn = " // sciloop message : mel :"+
				" 'sciDisplayCameraAttributesInChannelBox' :";

	if( size($inputArray)==0 )
	{
		$inputArray = sciGetSelectedNodeTypes( {"camera"}, {} );

		if( size($inputArray)==0 )
		{
			string $sel[] = ls( "-sl" );
			if( size($sel)!=0 )
			{
				print( $sn+" selection has no camera"+
				" nodeTypes. Terminate script!\n" );
				return;
			}

			string $types[] = nodeType( "-derived", "-isTypeName", "camera" );

			if( size($types)==0 )
			{
				print( $sn+" there is absolutely no camera type"+
						" in the scene. Terminate script!\n" );
				return;
			}

			for( $type in $types )
			{
				string $objects[] = {};

				if( catchQuiet($objects = lsType($type)) )
				{
					print( $sn+" could not get objects for a"+
						" nodeType named : '"+$type+"'.\n" );

					continue;
				}

				$inputArray = stringArrayCatenate( $inputArray, $objects );
			}
		}
	}

	// Set the camera attributes
	//
	string $cameraAttributes[] =
	{
		"angleOfView",          "cameraScale",
		"nearClipPlane",	    "farClipPlane",
		"filmGate",
		"filmAspectRatio",
		"fitResolutionGate",
		"filmFitOffset",        "horizontalFilmOffset", "verticalFilmOffset",
		"shakeEnabled",         "horizontalShake",      "verticalShake",
		"shakeOverscanEnabled", "shakeOverscan",
		"preScale",
		"filmTranslateH",       "filmTranslateV",
		"horizontalRollPivot",  "verticalRollPivot",
		"filmRollValue",
		"filmRollOrder",
		"postScale",
		"depthOfField",         "focusRegionScale",
		"tumblePivotX",         "tumblePivotY",         "tumblePivotZ",
		"usePivotAsLocalSpace",
		"orthographic",         "orthographicWidth"
	};

	for( $camera in $inputArray )
	{
		for( $attr in $cameraAttributes )
		{catchQuiet( setAttr( "-k", 1, $camera+"."+$attr ) );}
	}

	print( $sn+" done!\n" );

}

//_______________________________________________________________________________
//
//
