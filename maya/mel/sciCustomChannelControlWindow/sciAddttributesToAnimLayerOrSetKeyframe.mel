//
// We need this proc when we open the customized
// 'channelControl' window, so that we can add attributes
// to the selected animLayer from there.
// We also can set keyframes.

//_________________________________________________________________________
//
//
// Move items selected in the non-keyable list
// to the keyable list...
//
global proc sciAddttributesToAnimLayerOrSetKeyframe(	string $parent,
														string $selectedItem,
														string $animLayerOrKeyframe )

{
	string $sn = " sciloop message : mel : 'sciAddttributesToAnimLayerOrSetKeyframe' :";

//print( " in 'sciAddToAnimLayer\n" );

	 // Get the selected animLayers:
    //
    string $llayers[] = {};
    $llayers[0] = "";
    $llayers = getSelectedAnimLayer( "AnimationLayerEditor" );
    int $sizeAnimLayers = size( $llayers );

    if( $llayers[0]=="" && $animLayerOrKeyframe=="animLayer" )
    {
    	print( $sn+" No animLayer selected. Script execution aborted!\n" );
    	return;
    }

	setParent $parent;

	string $selectedAttrs[] = {};
	int $selectedIndices[] = {};

	int $selectedAttrsSize;

	// Get attributes from non-keyable list:
	//
	$selectedIndices = `textScrollList -q -sii NONKEYABLE_List`;
	$selectedAttrsSize = size( $selectedIndices );
	string $longNames[] = `textScrollList -q -ai NONKEYABLE_LONG_List`;


	if( $selectedAttrsSize==0 )
	 {
		$selectedIndices = `textScrollList -q -sii DISP_NONKEYABLE_List`;
		$selectedAttrsSize = size( $selectedIndices );
		$longNames = `textScrollList -q -ai DISP_NONKEYABLE_LONG_List`;
	}

	// Subtract one since the indices were filled up from 1-based
	// textScrollList -q -sii but $longNames requires 0-based
	// acess...
	//
	for( $index in $selectedIndices )
	{$selectedAttrs[size($selectedAttrs)] = $longNames[$index-1];}



	// Get attributes from keyable list:
	//
	clear( $selectedIndices );
	clear( $longNames );
	$selectedIndices = `textScrollList -q -sii KEYABLE_List`;
	$selectedAttrsSize = $selectedAttrsSize + size( $selectedIndices );
	$longNames = `textScrollList -q -ai KEYABLE_LONG_List`;


	if( $selectedAttrsSize==0 )
	 {
		$selectedIndices = `textScrollList -q -sii DISP_KEYABLE_List`;
		$selectedAttrsSize = $selectedAttrsSize + size( $selectedIndices );
		$longNames = `textScrollList -q -ai DISP_KEYABLE_LONG_List`;
	}

	// Subtract one since the indices were filled up from 1-based
	// textScrollList -q -sii but $longNames requires 0-based
	// acess...
	//
	for( $index in $selectedIndices )
	{$selectedAttrs[size($selectedAttrs)] = $longNames[$index-1];}



	// Since listAttr is going to return the full attribute names
	// we need, strip off any component parts of $selectedItem
	// by tokenizing based on the "." character.
	//
	string $node, $buffer[];
	tokenize( $selectedItem, ".", $buffer );

	if( size($buffer)>0 )
	{$node = $buffer[0];}
	else
	{$node = $selectedItem;}


    print( $animLayerOrKeyframe+" :\n" );
    for( $layer in $llayers )
    {print( "'"+$layer+"'\n" );}

    print( "\n the attributes :\n" );
    for( $attr in $selectedAttrs )
    {print( "'"+$attr+"'\n" );}

    print( "\n last selected node :\n" );
    print( "'"+$node+"'\n" );



	// see if the ALL option is engaged
	//
	int $changeAll = `checkBox -q -v CC_KEYALL_Box`;

	if ( $changeAll )
	{
		// get the node type of the current node
		//
		string $baseType = nodeType( $selectedItem );

		// get a list of all selected nodes
		//
		string $allNodes[] = selectedNodes();

		// for each node that matches type, set the keyability
		// of each attribute in the non-keyable list
		//
		int $limit = size( $allNodes );
		string $thisNode;
		int $index;


		if( $animLayerOrKeyframe=="animLayer" )
		{
	    	for( $i=0; $i<$sizeAnimLayers; $i++ )
		    {
				for( $index=0; $index<$limit; $index++ )
				{
					$thisNode = $allNodes[$index];

					if( nodeType($thisNode)==$baseType )
					{
						for ( $m=0; $m<$selectedAttrsSize; $m++ )
						{
							string $res = animLayer(    "-edit",
		                                                "-attribute", $thisNode+"."+$selectedAttrs[$m],
		                                                $llayers[$i] );
						}
					}
				}
			}
		}
		else if( $animLayerOrKeyframe=="keyframe" )
		{
			for( $index=0; $index<$limit; $index++ )
			{
				$thisNode = $allNodes[$index];

				if( nodeType($thisNode)==$baseType )
				{
					for ( $m=0; $m<$selectedAttrsSize; $m++ )
					{
						if( catchQuiet( setKeyframe( $thisNode+"."+$selectedAttrs[$m] ) ) )
						{print( $sn+" could not set keyframe for :"+$thisNode+"."+$selectedAttrs[$m] );}
					}
				}
			}
		}
		else
		{print( $sn+"oops!\n" );}
	}

	// only change the selected node
	//
	else
	{
		if( $animLayerOrKeyframe=="animLayer" )
		{
			for( $i=0; $i<$sizeAnimLayers; $i++ )
		    {
				for( $m=0; $m<$selectedAttrsSize; $m++ )
				{
					string $res = animLayer(    "-edit",
		        	                            "-attribute", $node+"."+$selectedAttrs[$m],
		                                    $llayers[$i] );
				}
			}
		}
		else if( $animLayerOrKeyframe=="keyframe" )
		{
			for( $i=0; $i<$sizeAnimLayers; $i++ )
		    {
				for( $m=0; $m<$selectedAttrsSize; $m++ )
				{
					if( catchQuiet( setKeyframe( $node+"."+$selectedAttrs[$m] ) ) )
						{print( $sn+" could not set keyframe for :"+$node+"."+$selectedAttrs[$m] );}
				}
			}
		}
		else
		{print( $sn+"oops!\n" );}
	}
}
